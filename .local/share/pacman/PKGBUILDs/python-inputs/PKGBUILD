# Maintainer: Aseem Athale <athaleaseem@gmail.com>
# Contributor: Major <majorx234@googlemail.com>

_base=inputs
pkgname=python-${_base}
pkgver=0.5
pkgrel=4
pkgdesc="Cross-platform Python support for keyboards, mice and gamepads"
arch=('i686' 'x86_64' 'armv7h')
url="https://github.com/zeth/${_base}"
license=(BSD)
makedepends=(python-build
             python-installer
             python-wheel
             python-setuptools)

source=("$pkgname-$pkgver.tar.gz"::${url}/archive/refs/tags/v${pkgver}.tar.gz
        3203c9e25f1e14c4316d85d59c536b4e407f569f.patch)
sha256sums=('d8e8a4b54747e102c4ab27f9eff0e6d64207690d7dbc75d2f6c6364881066fee'
            '92c69faff3038a42eb0fc4dfefe2657dea1e5cf0cea05c14d02ba04decdcc139')

prepare() {
    cd "${_base}-${pkgver}"
    patch -p1 -i "$srcdir/3203c9e25f1e14c4316d85d59c536b4e407f569f.patch"
}

build() {
    cd "${_base}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${_base}-${pkgver}"
    python -m installer --destdir="$pkgdir" dist/*.whl
    # Install LICENSE file
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    # Install README file
    install -Dm644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}
